plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
    id 'wtf.gofancy.fancygradle' version "${fancy_gradle_version}"
}

version = mod_version
group = mod_id
base.archivesName = mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            args "--username=Nischhelm"

            jvmArg "-Dfml.coreMods.load=${mixin_loader_class}"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {

            jvmArg "-Dfml.coreMods.load=${mixin_loader_class}"
            jvmArg "-Dmixin.hotSwap=true"
            jvmArg "-Dmixin.checks.interfaces=true"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = 'https://www.cursemaven.com' }
    maven { url = 'https://maven.blamejared.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly "org.spongepowered:mixin:${mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    compileOnly "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"

    implementation fg.deobf("curse.maven:fermiumbooter-${fermiumbooter_version}")
    compileOnly fg.deobf("curse.maven:waystones-${waystones_version}")
    compileOnly fg.deobf("curse.maven:antique-atlas-${antiqueatlas_version}")
    compileOnly fg.deobf("curse.maven:aarc-${aarc_version}")

    //Bruh
    compileOnly fg.deobf("curse.maven:iceandfire-${iceandfire_version}")
    compileOnly fg.deobf("curse.maven:iceandfire-rlcraft-${iceandfirerl_version}")
    compileOnly fg.deobf("curse.maven:iceandfire-rotn-${iceandfirerotn_version}")
    compileOnly fg.deobf("curse.maven:llibrary-${llibrary_version}")

    //Dungeons
    compileOnly fg.deobf("curse.maven:dungeons2-${dungeons2_version}")
    compileOnly fg.deobf("curse.maven:gottschcore-${gottschcore_version}")
    compileOnly fg.deobf("curse.maven:yungs-better-mineshafts-forge-${bettermineshafts_version}")
    compileOnly fg.deobf("curse.maven:ruins-${ruins_version}")
    compileOnly fg.deobf("curse.maven:lycanitesmobs-${lycanites_version}")
    compileOnly fg.deobf("curse.maven:doomlikedungeons-${doomlikedungeons_version}")
    compileOnly fg.deobf("curse.maven:fnars-roguelike-dungeons-${fnarsroguelikedungeons_version}")
    compileOnly fg.deobf("curse.maven:atomicstrykers-battle-towers-${battletowers_version}")
    compileOnly fg.deobf("curse.maven:quark-${quark_version}")
    //compileOnly fg.deobf("curse.maven:roguelike-dungeons-${roguelikedungeons_version}")

    //Tiles
    compileOnly fg.deobf("curse.maven:betterendforge-backport-${betterend_version}")
    compileOnly fg.deobf("curse.maven:open-terrain-generator-${otg_version}")
    compileOnly fg.deobf("curse.maven:traverse-reforged-${traverse_version}")
    compileOnly fg.deobf("curse.maven:defiledlands-${defiledlands_version}")
    compileOnly fg.deobf("curse.maven:betternether-${betternether_version}")
    compileOnly fg.deobf("curse.maven:thaumcraft-${thaumcraft_version}")
    compileOnly fg.deobf("curse.maven:biomes-o-plenty-${bop_version}")
    compileOnly fg.deobf("maven.modrinth:nether-api:${netherapi_version}")

    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-${crafttweaker_version}")
    compileOnly fg.deobf("CraftTweaker2:ZenScript:${zs_version}")
    compileOnly fg.deobf("CraftTweaker2:CraftTweaker2-API:${zs_version}")
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

fancyGradle {
    patches {
        resources
        coremods
        asm
        mergetool
    }
}

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'version': mod_version,
            'mcversion': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

jar {
    manifest {
        def attr = [:]
        attr['Specification-Title']         = mod_name
        attr['Specification-Vendor']        = mod_author
        attr['Specification-Version']       = '1'
        attr['Implementation-Title']        = mod_name
        attr['Implementation-Vendor']       = mod_author
        attr['Implementation-Version']      = mod_version
        attr['Implementation-Timestamp']    = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attr['FMLCorePlugin']               = mixin_loader_class
        attr['FMLCorePluginContainsFMLMod'] = 'true'
        attr['TweakClass']                  = 'org.spongepowered.asm.launch.MixinTweaker'
        attr['ForceLoadAsMod'] = "true"

        def currentTasks = gradle.startParameter.taskNames
        if (currentTasks[0] == 'build' || currentTasks[0] == 'assemble')
            attr['ForceLoadAsMod'] = "true"
        attributes(attr)
    }
}

tasks.register("copyReobfJar", Copy) {
    dependsOn("reobfJar")

    from(layout.buildDirectory.file("reobfJar/output.jar"))
    into(layout.projectDirectory.dir("run/mods"))
    rename("output.jar", "${mod_name}-${mod_version}.jar")
}

afterEvaluate {
    tasks.matching { it.name.startsWith("reobfJar") }.configureEach {
        finalizedBy("copyReobfJar")
    }
}

jar.finalizedBy('reobfJar')